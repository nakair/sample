buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.0.1.RELEASE'
    }
}

allprojects {
    apply plugin : 'java'
    apply plugin: 'spring-boot'

    repositories {
        mavenCentral()
    }

    [compileJava, compileTestJava].each {
        it.sourceCompatibility = 1.8
        it.targetCompatibility = 1.8
        it.options.encoding = 'UTF-8'
    }

    configurations {
        provided
        compile.extendsFrom provided
        all*.exclude group: "commons-logging", module: "commons-logging"
    }

    ext {
        springVersion = '4.0.3.RELEASE'
        slf4jVersion = '1.7.7'
        logbackVersion = '1.1.2'

        springDataJpaVersion = '1.5.1.RELEASE'
        springDataRestVersion = '2.0.1.RELEASE'
        hibernateVersion = '4.3.5.Final'

        junitVersion = '4.11'
        mockitoVersion = '1.9.5'
        h2Version = '1.3.173'

        servletApiVersion = '3.0.1'
        cglibVersion = '3.1'
        asmVersion = '3.3.1'
        aspectjVersion = '1.7.3'
    }

    dependencies {
    }
}

configure(rootProject) {
    task wrapper(type: Wrapper) {
        description = "Generates gradlew[.bat] scripts"
        gradleVersion = "1.11"
    }
}

configure(subprojects) { subproject ->
    group = 'jp.co.itrade.sample'
    defaultTasks 'clean', 'packages'
    jar {
        doFirst {
            manifest.attributes["Created-By"] =  "${System.getProperty("java.version")}"
            manifest.attributes["Implementation-Title"] = subproject.name
            manifest.attributes["Implementation-Version"] = subproject.version
        }
    }
}

project("sample-domain") {
    description = 'domain'
    defaultTasks 'clean', 'packages'
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "javax.inject:javax.inject:1"
        compile "com.h2database:h2"
        provided "org.projectlombok:lombok:1.12.+"
    }
}
project("sample-web") {
    description = 'web'
    defaultTasks 'clean', 'packages'
    dependencies {
        compile project(':sample-domain')
        compile("org.springframework.boot:spring-boot-starter-web") {
            exclude module: "spring-boot-starter-tomcat"
        }
        compile "org.eclipse.jetty:jetty-webapp:9.1.4.v20140401"
    }
}
