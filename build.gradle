buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE'
    }
}

allprojects {
    apply plugin : 'java'
    apply plugin : 'eclipse'
    apply plugin : 'idea'
    apply plugin : 'spring-boot'

    repositories {
        mavenCentral()
    }

    [compileJava, compileTestJava].each {
        it.sourceCompatibility = 1.8
        it.targetCompatibility = 1.8
        it.options.encoding = 'UTF-8'
    }

    configurations {
        provided
        compile.extendsFrom provided
        all*.exclude group: "commons-logging", module: "commons-logging"
    }
}

configure(subprojects) { subproject ->
    group = 'jp.co.itrade.sample'
    version = 0.1
    defaultTasks 'clean', 'packages'
    jar {
        doFirst {
            manifest.attributes["Created-By"] =  "${System.getProperty("java.version")}"
            manifest.attributes["Implementation-Title"] = subproject.name
            manifest.attributes["Implementation-Version"] = subproject.version
        }
    }
}

project("sample-domain") {
    description = 'domain'
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "javax.inject:javax.inject:1"
        compile "com.h2database:h2"
        provided "org.projectlombok:lombok:1.12.+"
    }
}
project("sample-web") {
    description = 'web'
    dependencies {
        compile project(':sample-domain')
        compile("org.springframework.boot:spring-boot-starter-web") {
            exclude module: "spring-boot-starter-tomcat"
        }
        compile "org.eclipse.jetty:jetty-webapp:9.1.+"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.3.+"
    }
}
project("sample-boot") {
    description = 'boot'
    dependencies {
        compile project(':sample-domain'), project(':sample-web')
    }
}
